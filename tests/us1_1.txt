# User Story 1 - Criação de conta
# Permita a um usuário criar uma conta no Jackut.
# Deve ser fornecido um login, uma senha e um nome com o qual o usuário será conhecido na rede.

zerarSistema

# Obs.: usar codificação de caracteres ISO 8859

expectError "Usuário não cadastrado." getAtributoUsuario login=jpsauve atributo=nome
criarUsuario login=jpsauve senha=sauvejp nome="Jacques Sauve"
expect "Jacques Sauve" getAtributoUsuario login=jpsauve atributo=nome

# O comando "abrirSessao" retorna um id que pode ser atribuído a uma variável do EasyAccept para referência posterior no script, como você poderá ver, por exemplo, no user story 2.
# A gente criou esse comando por questão de segurança, para simular um usuário entrando a senha e iniciando um modo privado de uso do sistema.
# Nesse caso da us1_1, ele está sendo usado meramente para testar a senha (porque não podemos usar "expect getAtributoUsuario atributo=senha", justamente pelas questões de segurança).
# Se a senha estiver correta, a sessão é aberta e o teste passa sem erros.
# Se a senha não estiver implementada corretamente (ou estiver errada), o programa vai lançar uma exceção e o EasyAccept vai acusar erro nessa linha.

abrirSessao login=jpsauve senha=sauvejp

###################
# Testes de todas as outras combinações importantes para o comando criarUsuario
###################
# Cada usuário está associado a um único login, com o qual é identificado no sistema; dois usuários não podem ter o mesmo login

expectError "Conta com esse nome já existe." criarUsuario login=jpsauve senha=##$@!# nome="Jacques Sauve II"

# Entretanto, se o login for diferente, dois usuários podem ter o mesmo nome

criarUsuario login=jpsauve2 senha=sauvejp nome="Jacques Sauve"

expect "Jacques Sauve" getAtributoUsuario login=jpsauve2 atributo=nome
abrirSessao login=jpsauve2 senha=sauvejp

expectError "Login inválido." criarUsuario login= senha=fasdh nome="Jacques Sauve"
expectError "Senha inválida." criarUsuario login=jpsauve3 senha= nome="Jacques Sauve"
# Nome vazio é permitido
criarUsuario login=jpsauve3 senha=3sauvejp nome=""

###################
# Testes de todas as outras combinações importantes para o comando abrirSessao
###################
# Observe que ao dar erro, não queremos dizer se o erro foi login ou senha
# Isso é a forma padrão de tratar o assunto, pois, desta forma, o hacker tem que adivinhar ambos simultaneamente, em vez de quebrar login e senha separadamente

expectError "Login ou senha inválidos." abrirSessao login=jpsauve senha=x
expectError "Login ou senha inválidos." abrirSessao login=x senha=x
expectError "Login ou senha inválidos." abrirSessao login= senha=x
expectError "Login ou senha inválidos." abrirSessao login=jpsauve senha=

encerrarSistema